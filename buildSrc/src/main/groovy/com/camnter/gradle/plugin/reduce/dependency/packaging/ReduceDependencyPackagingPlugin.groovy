package com.camnter.gradle.plugin.reduce.dependency.packaging

import com.android.build.gradle.AppExtension
import com.android.build.gradle.AppPlugin
import com.android.build.gradle.api.ApplicationVariant
import com.camnter.gradle.plugin.reduce.dependency.packaging.hooker.TaskHookerManager
import com.camnter.gradle.plugin.reduce.dependency.packaging.transform.ReduceDependencyPackagingTransform
import org.gradle.api.Plugin
import org.gradle.api.Project
import org.gradle.api.plugins.ExtensionContainer
import org.gradle.internal.reflect.Instantiator
import org.gradle.tooling.provider.model.ToolingModelBuilderRegistry

import javax.inject.Inject

/**
 * @author CaMnter
 */

class ReduceDependencyPackagingPlugin implements Plugin<Project> {

    private Instantiator instantiator

    /**
     * Stores files generated by the host side and is used when building plugin apk
     * */
    private def hostDir

    /**
     * TaskHooker manager, registers hookers when apply invoked
     * */
    private TaskHookerManager taskHookerManager

    @Inject
    public ReduceDependencyPackagingPlugin(Instantiator instantiator,
            ToolingModelBuilderRegistry registry) {
        this.instantiator = instantiator
    }

    @Override
    void apply(Project project) {
        final ExtensionContainer extensions = project.extensions
        if (!project.plugins.hasPlugin(AppPlugin.class)) {
            println "[ReduceDependencyPackagingPlugin]   reduce-dependency-packaging-plugin requires the Android plugin to be configured"
            return
        }

        taskHookerManager = new TaskHookerManager(project, instantiator)
        taskHookerManager.registerTaskHookers()

        project.extensions.create('reduceDependencyPackagingExtension',
                ReduceDependencyPackagingExtension)
        final AppExtension android = extensions.getByType(AppExtension.class)

        initExtension(project, android)
        android.registerTransform(
                new ReduceDependencyPackagingTransform(project, android.applicationVariants))
    }

    def initExtension(Project project, AppExtension android) {
        hostDir = new File(project.rootDir, "host")
        if (!hostDir.exists()) {
            hostDir.mkdirs()
        }
        final ReduceDependencyPackagingExtension reduceDependencyPackagingExtension = project.reduceDependencyPackagingExtension
        project.afterEvaluate {
            android.applicationVariants.each { ApplicationVariant variant ->
                reduceDependencyPackagingExtension.with {
                    packageName = getApplicationId(project, variant)
                    packagePath = packageName.replace('.'.charAt(0), File.separatorChar)
                    hostSymbolFile = new File(hostDir, "Host_R.txt")
                    hostDependenceFile = new File(hostDir, "versions.txt")
                }
            }
        }
    }
}